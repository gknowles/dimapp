; cmdline-argv.abnf - dim services
;
; rules to parse a command line into an argv array
%root = line
%api.prefix = Argv
%api.file.h = cmdline-argv.h            ; defaults to <%api.prefix>parse.h
%api.file.cpp = cmdline-argv.cpp        ; defaults to <%api.prefix>parse.cpp

; command line to argv grammer rules
line = OWS closed-param *(RWS closed-param) [RWS open-param] OWS
closed-param = *(unquoted / fully-quoted) *BSLASH              { Start, End }
open-param = *(unquoted / fully-quoted) half-quoted *BSLASH    { Start, End }
fully-quoted = active-quote quoted active-quote
half-quoted = active-quote quoted
unquoted = 1*( ptext / escaped-quote )
quoted = 1*( ptext / dquote-pair / escaped-quote / RWS )
ptext = 1*( [BSLASH] pchar )
escaped-quote = BSLASH *bslash-pair DQUOTE
active-quote = *bslash-pair DQUOTE
dquote-pair = DQUOTE DQUOTE
bslash-pair = BSLASH BSLASH
pchar = %x61 ; %x21 / %x23-5b / %x5d-7e
RWS = 1*HTAB ; 1*(SP / HTAB)
OWS = *HTAB ; *(SP / HTAB)
BSLASH = %x5c


t1 = t1-param *(RWS t1-param)
t1-param = RWS { Start, End }

; t2 - requires infinite stack. A series of 10 alphas, may be either 1 (if 
; followed by quote) or 10 (if followed by EOF) params 
t2 = *t2-param
t2-param = *(1*ALPHA DQUOTE) ALPHA { Start, End }

